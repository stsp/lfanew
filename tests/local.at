dnl Copyright (c) 2023 TK Chia
dnl
dnl Redistribution and use in source and binary forms, with or without
dnl modification, are permitted provided that the following conditions are
dnl met:
dnl
dnl   * Redistributions of source code must retain the above copyright
dnl     notice, this list of conditions and the following disclaimer.
dnl   * Redistributions in binary form must reproduce the above copyright
dnl     notice, this list of conditions and the following disclaimer in the
dnl     documentation and/or other materials provided with the distribution.
dnl   * Neither the name of the developer(s) nor the names of its
dnl     contributors may be used to endorse or promote products derived from
dnl     this software without specific prior written permission.
dnl
dnl THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
dnl IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
dnl TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
dnl PARTICULAR PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT
dnl HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
dnl SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
dnl TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
dnl PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
dnl LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
dnl NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
dnl SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

m4_define([AT_LFANEW_TOOL],["$abs_top_builddir"/lfanew])
m4_define([AT_LFANEW_AAIN],[./a.asm])
m4_define([AT_LFANEW_ACIN],[./a.c])
m4_define([AT_LFANEW_AOUT],[./a.exe])
m4_define([AT_LFANEW_A2OUT],[./a2.exe])
m4_define([AT_LFANEW_TMP],[./a.tmp])

dnl $1 = text of program to assemble
m4_define([AT_LFANEW_BINASM],[dnl
AT_DATA([AT_LFANEW_AAIN],[$1])
AT_CHECK([["$NASM" -o]AT_LFANEW_AOUT AT_LFANEW_AAIN],,[[ignore]],[[ignore]])])

dnl $1 = text of program to compile
dnl $2 = extra compiler options
m4_define([AT_LFANEW_WINCC],[dnl
AT_DATA([AT_LFANEW_ACIN],[$1])
AT_CHECK([[$WINCC $WINCFLAGS -o]AT_LFANEW_AOUT $2 AT_LFANEW_ACIN dnl
[$WINLDLIBS]],,[[ignore]],[[ignore]])])

dnl $1 = text of program to compile
dnl $2 = section alignment to use
m4_define([AT_LFANEW_WINCC_REALIGN],[dnl
[if test yes = "$lfanew_cv_prog_wincc_wl_align"; then]
  AT_LFANEW_WINCC([$1],[[-Wl,-align:]$2])
[else]
  AT_LFANEW_WINCC([$1],[[-Wl,--section-alignment=]$2])
[fi]])

dnl $1 = text of program to compile
dnl $2 = extra compiler options
m4_define([AT_LFANEW_HOSTCC],[dnl
AT_DATA([AT_LFANEW_ACIN],[$1])
AT_CHECK([[$CC $CPPFLAGS -I"$abs_top_builddir"/h -I"$abs_top_srcdir"/h] dnl
[$CFLAGS $LDFLAGS -o]AT_LFANEW_AOUT $2 AT_LFANEW_ACIN [$LDLIBS]],,dnl
[[ignore]],[[ignore]])])

dnl $1 = program to run
dnl $2 = expected stdout output
m4_define([AT_LFANEW_DOSRUN],[dnl
AT_CHECK([["$DOSRUN"] $1 [>] AT_LFANEW_TMP ]dnl
	  [[&& "$DOS2UNIX" <] AT_LFANEW_TMP],,[$2],[[ignore]])])

dnl $1 = program to run
dnl $2 = expected stdout output
m4_define([AT_LFANEW_WINRUN],[dnl
if test -z "$WINRUN"; then
  AT_CHECK([$1 [>] AT_LFANEW_TMP ]dnl
	   [[&& "$DOS2UNIX" <] AT_LFANEW_TMP],,[$2],[[ignore]])
else
  AT_CHECK([["$WINRUN"] $1 [>] AT_LFANEW_TMP ]dnl
	   [[&& "$DOS2UNIX" <] AT_LFANEW_TMP],,[$2],[[ignore]])
fi])

dnl old-style MZ stub with only .e_cblp & .e_cp
m4_define([AT_LFANEW_MZSTUB],[[
%define MZ_PG_SZ	0x200
%define MZ_PARA_SZ	0x10
%define MZ_PSP_SZ	0x100
mz_start:
	dw	0x5a4d						; .e_magic
	dw	(mz_end - mz_start) % MZ_PG_SZ			; .e_cblp
	dw	(mz_end - mz_start + MZ_PG_SZ - 1) / MZ_PG_SZ	; .e_cp
	dw	0						; .e_crlc
	dw	(mz_hdr_end - mz_start) / MZ_PARA_SZ		; .e_cparhdr
	dw	0						; .e_minalloc
	dw	-1						; .e_maxalloc
	dw	-(MZ_PSP_SZ / MZ_PARA_SZ), MZ_PSP_SZ		; .e_ss, .e_sp
	dw	0						; .e_csum
	dw	mz_entry					; .e_ip
	dw	-((mz_hdr_end - mz_start) / MZ_PARA_SZ)		; .e_cs
	dw	mz_relocs					; .e_lfarlc
	dw	0						; .e_ovno
mz_relocs:
	align	MZ_PARA_SZ
mz_hdr_end:
mz_msg:
	db	"world, hello",13,10,"$"
mz_entry:
	push	cs
	pop	ds
	mov	ah, 9
	mov	dx, mz_msg
	int	0x21
	mov	ax, 0x4c00
	int	0x21
mz_end:
]])
